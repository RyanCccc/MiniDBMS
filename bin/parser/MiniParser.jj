options {
	JAVA_UNICODE_ESCAPE = true;
	STATIC = false;
}
PARSER_BEGIN(MiniParser)

	package parser;
	
	import java.util.*;
	import mysql.*;

	public class MiniParser {

	}
PARSER_END(MiniParser)

// Lexical definitions --------------------------------------------------------

// whitespace
SKIP :
{
    <" "> | <"\t"> | <"\n"> | <"\f"> | <"\r">
}

// SQL keywords
TOKEN [IGNORE_CASE]:
{
	< KW_AND: "AND" >
|	< KW_OR: "OR" >
|	< KW_CREATE: "CREATE" >
|	< KW_CHECK: "CHECK" >
|	< KW_PRIMARY: "PRIMARY" >
|	< KW_FOREIGN: "FOREIGN" >
|	< KW_KEY: "KEY" >
|	< KW_REFERENCES: "REFERENCES" >
|	< KW_DROP: "DROP" >
|	< KW_TABLE: "TABLE" >
|	< KW_SELECT: "SELECT" >
|	< KW_FROM: "FROM" >
|	< KW_WHERE: "WHERE" >
|	< KW_INSERT: "INSERT" >
|	< KW_INTO: "INTO" >
|	< KW_VALUES: "VALUES" >
|	< KW_DELETE: "DELETE" >
|	< KW_UPDATE: "UPDATE" >
|	< KW_SET: "SET" >
|	< KW_HELP: "HELP" >
|	< KW_TABLES: "TABLES" >
|	< KW_DESCRIBE: "DESCRIBE" >
|	< KW_QUIT: "QUIT" >

|	< KW_USER: "USER" >
|	< KW_USERA: "USER-A" >
|	< KW_USERB: "USER-B" >
|	< KW_SUBSCHEMA: "SUBSCHEMA" >

|	< KW_INT: "int" >
|	< KW_CHAR: "char" >
|	< KW_DECIMAL: "decimal" >
}

// SQL symbols
TOKEN:
{
	< SYM_PLUS: "+" >
|	< SYM_MINUS: "-" >
|	< SYM_STAR: "*" >
|	< SYM_SLASH: "/" >
|	< SYM_EQUAL: "=" >
|	< SYM_EXCLEQUAL: "!=" >
|	< SYM_LESS: "<" >
|	< SYM_MORE: ">" >
|	< SYM_LESSEQUAL: "<=" >
|	< SYM_MOREEQUAL: ">=" >
}

// SQL identifiers
TOKEN:
{
	< IDENTIFIER: ["a"-"z", "A"-"Z"](["_", "0"-"9", "a"-"z", "A"-"Z"])* >
}

// literals
TOKEN:
{
	< INT_LITERAL: (["0"-"9"])+ >
|	< DECIMAL_LITERAL: (["0"-"9"])*"."(["0"-"9"])+ >
|	< STRING_LITERAL: "'"(~["\\", "'"]|"''"|"\\'")*"'" >
	// does not allow escapes except for escaping a single quote
}




// Grammar --------------------------------------------------------------------
Command Command():
{
	Command ret;
	ret = new Command();
}
{
	(
		< KW_CREATE >
		(
		  ret = createTable()
		| ret = createUser()
		| ret = createSubschema() 
		  
		)
	|	ret = DropTable()
	|	ret = Select()
	|	ret = Insert()
	
	|	< KW_DELETE >
		(
			ret = delete()
		|	ret = deleteUser()
		|	ret = deleteSubschema()
		)
	|	ret = Update()
	
	|	< KW_HELP >
		(
		  	ret = helpTable()
		 |  ret = helpDescribe()
		 |  ret = helpCmd()
		)
	) ";" < EOF >
	{ return ret; }
}

//Create Subschema
CreateSubschemaCommand createSubschema():
{
	String tableName;
	List<String> attrNames = new ArrayList<String>();
	String attrName;
}
{
	< KW_SUBSCHEMA > ( tableName = getTokenImage() )
	( ( attrName = getTokenImage() ) { attrNames.add(attrName); } )
	( "," ( attrName = getTokenImage() ) { attrNames.add(attrName); } )*
	{ return new CreateSubschemaCommand(tableName, attrNames); }
}

//Create User
CreateUserCommand createUser():
{
	String userName;
	User.Type userType;
}
{
	< KW_USER > ( userName = getTokenWithCase() )
	(
		< KW_USERA > { userType = User.Type.USER_A; }
	|	< KW_USERB > { userType = User.Type.USER_B; }
	)
	{ return new CreateUserCommand(userName, userType); }
}

//Create Table
CreateTableCommand createTable():
{
  	String tableName;
	List<Attribute> attributes = new ArrayList<Attribute>();
	List<String> primaryKeys = new ArrayList<String>();
	List<ForeignKey> foreignKeys = new ArrayList<ForeignKey>();

	Attribute attr;
	ForeignKey foreignKey;

	String name;
}
{
  < KW_TABLE > ( tableName = getTokenImage() )
	"("
	(
		(
			( attr = parseAttr() ) ","
			{ attributes.add(attr); }
		)+
		(
			< KW_PRIMARY > < KW_KEY >
			"("
				( name = getTokenImage() { primaryKeys.add(name); } )
				( "," name = getTokenImage() { primaryKeys.add(name); } )*
			")"
		)
		(
			"," ( foreignKey = parseForeignKey() )
			{ foreignKeys.add(foreignKey); }
		)*
	)
	")"
	{ return new CreateTableCommand(tableName, attributes, primaryKeys, foreignKeys); }
}

Attribute parseAttr():
{
  	String name;
	Attribute.Type type;
	int length = -1;
	String constraint = "";}
{
	( name = getTokenImage() )
	(			
		(
			< KW_INT > { type = Attribute.Type.INT; }
		)
	|	(
			< KW_CHAR > "(" < INT_LITERAL > { length=Integer.parseInt(token.image); } ")"
			{ type = Attribute.Type.CHAR; } 
		)
	|	(
			< KW_DECIMAL > { type = Attribute.Type.DECIMAL; }
		)
	)
	(
		// the constraint exp should have an expString generated
		< KW_CHECK >
		"(" (
				constraint = Expression()
		) ")"
	)?
	{ return new Attribute(type, name, length, constraint); }}

ForeignKey parseForeignKey():
{
  	String refTableName;
	String localAttrName;
	String refAttrName;}
{
  	< KW_FOREIGN > < KW_KEY >
	"("
	( localAttrName = getTokenImage() )
	")"
	< KW_REFERENCES >
	( refTableName = getTokenImage() )
	"("
	( refAttrName = getTokenImage() )
	")"
	{ return new ForeignKey(refTableName, localAttrName, refAttrName); }}

//Delete
DeleteCommand delete():
{
	String tableName;
	String condition = "";
}
{
	< KW_FROM >
	( tableName = getTokenImage() )
	( < KW_WHERE > ( condition = Expression() ) )?
	{ return new DeleteCommand(tableName, condition); }
}

//Delete User
DeleteUserCommand deleteUser():
{
	String userName;
}
{
	< KW_USER > ( userName = getTokenWithCase() )
	{ return new DeleteUserCommand(userName); }
}

//Delete subschema
DeleteSubschemaCommand deleteSubschema():
{
	String tableName;
}
{
	< KW_SUBSCHEMA > ( tableName = getTokenImage() )
	{ return new DeleteSubschemaCommand(tableName); }
}

//Help Table
HelpTableCommand helpTable():
{
}
{
	< KW_TABLES >
	{ return new HelpTableCommand(); }
}

//Help Describe
HelpDescribeCommand helpDescribe():
{
	String tableName;
}
{
	< KW_DESCRIBE > ( tableName = getTokenImage() )
	{ return new HelpDescribeCommand(tableName); }
}

HelpCmdCommand helpCmd():
{
	HelpCmdCommand.Type type;
}
{
	(
		(
			< KW_CREATE > < KW_TABLE >
			{ type = HelpCmdCommand.Type.CREATE_TABLE; }
		)
	|	(
			< KW_DROP > < KW_TABLE >
			{ type = HelpCmdCommand.Type.DROP_TABLE; }
		)
	|	(
			< KW_SELECT >
			{ type = HelpCmdCommand.Type.SELECT; }
		)
	|	(
			< KW_INSERT >
			{ type = HelpCmdCommand.Type.INSERT; }
		)
	|	(
			< KW_DELETE >
			{ type = HelpCmdCommand.Type.DELETE; }
		)
	|	(
			< KW_UPDATE >
			{ type = HelpCmdCommand.Type.UPDATE; }
		)
	)
	{ return new HelpCmdCommand(type); }
}

//Drop table
DropTableCommand DropTable():
{
  String tableName;}
{
  	< KW_DROP > < KW_TABLE > ( tableName = getTokenImage() )
	{
	  return new DropTableCommand(tableName);
	}}

//Select statement
SelectCommand Select():
{
	List<String> attrNames = null;
	List<String> tables = new ArrayList<String>();
	String condition = "";

	String name;
}
{
	< KW_SELECT >
	(	
		(
			(
				( name = getTokenImage() )
				{
					attrNames = new ArrayList<String>();
					attrNames.add(name);
				}
			)
			( "," ( name = getTokenImage() ) { attrNames.add(name); } )*
		)
	|	< SYM_STAR >
	)
	< KW_FROM >
	( ( name = getTokenImage() ) { tables.add(name); } )
	( "," ( name = getTokenImage() ) { tables.add(name); } )*
	( < KW_WHERE > ( condition = Expression() ) )?
	{ return new SelectCommand(attrNames, tables, condition); }
}

//Insert into table
InsertCommand Insert():
{
	String tableName;
	List<Value> values = new ArrayList<Value>();

	Value value;
}
{
	< KW_INSERT > < KW_INTO > ( tableName = getTokenImage() )
	< KW_VALUES > "("
	( ( value = getValue() ) { values.add(value); } )
	( "," ( value = getValue() ) { values.add(value); } )*
	")"
	{ return new InsertCommand(tableName, values); }
}

//Update values
UpdateCommand Update():
{
	String tableName;
	List<AttributeAssign> attrAssgs = new ArrayList<AttributeAssign>();
	String condition = "";
	AttributeAssign attrAssg;
}
{
	< KW_UPDATE > ( tableName = getTokenImage() )
	< KW_SET > ( ( attrAssg = AttributeAssg() ) { attrAssgs.add(attrAssg); } )
	( "," ( attrAssg = AttributeAssg() ) { attrAssgs.add(attrAssg); } )*
	( < KW_WHERE > ( condition = Expression() ) )?
	{ return new UpdateCommand(tableName, attrAssgs, condition); }
}

AttributeAssign AttributeAssg():
{
  	String attrName;
	Value value;}
{
  	( attrName = getTokenImage() )
	< SYM_EQUAL > ( value = getValue() )
	{ return new AttributeAssign(attrName, value); }}

Value getValue():
{
  Attribute.Type type;
  String val;
}{
  (    
  	< INT_LITERAL >
  	{
  	  	type = Attribute.Type.INT;
  	  	val = token.image;  	}
  | < DECIMAL_LITERAL >
  	{		type = Attribute.Type.DECIMAL;
		val = token.image;
	}
  | < STRING_LITERAL >
  	{
		type = Attribute.Type.CHAR;
		val = token.image;
	}
  )
	{	  return new Value(type, val);
	}}

// Expression
String Expression():
{
  String exp;}
{
  (
    exp = getSingleExpression()
	(
	  (    	< KW_AND >
    	{    	  exp += " && ";
    	  exp += getSingleExpression();
    	}
   	  )
   	| (
   	  	< KW_OR >
   	  	{
    	  exp += " || ";
    	  exp += getSingleExpression();
    	}
   	  )  	)*
  )
  {    return exp;
  }}

String getSingleExpression():
{
  String exp = "";}
{
  (
    (
    	"("
    	{
      		exp += "(";
      		exp += getTokenImage();
    	}
    	(
        	< SYM_PLUS > | < SYM_MINUS > | < SYM_STAR > | < SYM_SLASH >
    	)
        {
          	exp += token.image;
        }
    	(
       		< INT_LITERAL > | < DECIMAL_LITERAL > | < STRING_LITERAL >	| < IDENTIFIER >
    	)
    	{
          		exp += token.image;
        }
    	")"
    	{
      		exp += ")";
    	}
  	)
  | 	{
  	  		exp += getTokenImage();
  		}  )
  (    
    < SYM_EQUAL >
  {
	  exp += "=";
  }
|	< SYM_EXCLEQUAL >
|	< SYM_LESS >
|	< SYM_MORE >
|	< SYM_LESSEQUAL >
|	< SYM_MOREEQUAL >
  )
  {
	  exp += token.image;
  }
  (
    < INT_LITERAL > | < DECIMAL_LITERAL > | < STRING_LITERAL > | < IDENTIFIER >
  )
  {
    exp += token.image;
  }
  {    return exp;
  }}

//Utility
String getTokenImage():
{}
{
	< IDENTIFIER >
	{ return token.image.toUpperCase(); }
}

String getTokenWithCase():
{}
{
	< IDENTIFIER >
	{ return token.image; }
}